import os

# Set search paths for Augur
search_paths = [
    os.getcwd(),
    os.path.join(workflow.basedir, "defaults"),
]
os.environ['AUGUR_SEARCH_PATHS'] = ":".join(search_paths)

include: "../shared/vendored/snakemake/config.smk"

configfile: os.path.join(workflow.basedir, "defaults/config.yaml")

if os.path.exists("config.yaml"):
    configfile: "config.yaml"

include: "rules/config.smk"

builds = get_builds()

wildcard_constraints:
    build = "genome|N450"

rule all:
    input:
        auspice_json = expand("auspice/measles_{build}.json", build=builds),
        tip_frequencies_json = expand("auspice/measles_{build}_tip-frequencies.json", build=builds)

include: "rules/prepare_sequences.smk"
include: "rules/prepare_sequences_N450.smk"
include: "rules/construct_phylogeny.smk"
include: "rules/annotate_phylogeny.smk"
include: "rules/export.smk"

# Include custom rules defined in the config.
if "custom_rules" in config["USER_CONFIG"]:
    for rule_file in config["USER_CONFIG"]["custom_rules"]:

        include: os.path.join(os.getcwd(), rule_file)

rule clean:
    """Removing directories: {params}"""
    params:
        "results ",
        "auspice"
    shell:
        "rm -rfv {params}"
